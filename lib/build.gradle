plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.jherkenhoff'

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ''
                arguments "-DANDROID_ARM_NEON=TRUE","-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
            version '3.10.2'
        }
    }
    buildFeatures {
        viewBinding true
    }
    ndkVersion '23.1.7779620'
    buildToolsVersion '33.0.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId rootProject.name
                version = rootProject.version

                pom {
                    name = rootProject.name
                    description = 'Port and precompiled binaries of the qalculate c library for android'
                    url = 'https://github.com/jherkenhoff/libqalculate-android'
                    packaging = 'aar'
                    inceptionYear = '2023'
                    licenses {
                        license {
                            name = 'GPL-2.0'
                            url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = "jherkenhoff"
                            name = "Jost Herkenhoff"
                            email = "jost.herkenhoff@mailbox.org"
                        }
                    }
                    scm {
                        url = 'https://github.com/jherkenhoff/libqalculate-android'
                        connection = 'https://github.com/jherkenhoff/libqalculate-android.git'
                    }
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.release
    }
}
